Name     ATA-IDE-LOGIC;
PartNo   00;
Date     28/03/2021;
Revision 01;
Designer MRVM;
Company  GTA;
Assembly None;
Location U2;
Device   V750C;         /* For ATF750C use V750C or V750CPPK (pin-keeper) else use G22V10 for GAL */
 
/* *************** INPUT PINS *********************/
PIN 1 = CLOCK;
PIN 2 = A4;
PIN [3..5] = [FC0..2];
PIN 6 = !LDS;
PIN 7 = !UDS;
PIN [8..10] = [A3..1];
PIN 13 = R_nW;
PIN 22 = !SELECT;

/* *************** OUTPUT PINS *********************/
PIN 15 = DTACK;
PIN 16 = !LSEL;
PIN 17 = !USEL;
PIN 18 = !RD;
PIN 19 = !WR;
PIN 20 = !CS1;
PIN 21 = !CS3;
PIN 23 = !VPA;

/* *************** INTERNAL NODES *********************/
NODE DTACK_ENABLE;
NODE Cb, Ca;

/* *************** EQUATIONS *********************/
DTACK.sp = 'b'0;
DTACK_ENABLE.sp = 'b'0;
DTACK.oe = DTACK_ENABLE;
Cb.sp = 'b'0;
Ca.sp = 'b'0;

DS = UDS # LDS;
FIELD STATE = [Cb, Ca, DTACK, DTACK_ENABLE];
STATE.ckmux = CLOCK;

SEQUENCE STATE {
    PRESENT 'b'0000
        IF SELECT & DS & R_nW
            NEXT 'b'1100;
        IF SELECT & DS & !R_nW
            NEXT 'b'0001;
        DEFAULT NEXT 'b'0000;
    /* READ Sequence */
    PRESENT 'b'1100
        NEXT 'b'1101;
    PRESENT 'b'1101
        IF !DS
            NEXT 'b'1111;
        DEFAULT NEXT 'b'1101;
    PRESENT 'b'1111
        NEXT 'b'1110;
    PRESENT 'b'1110
        NEXT 'b'0000;
    /* WRITE Sequence */
    PRESENT 'b'0001
        IF !DS
            NEXT 'b'0011;
        DEFAULT NEXT 'b'0001;
    PRESENT 'b'0011
        NEXT 'b'0010;
    PRESENT 'b'0010
        NEXT 'b'0000;
    /* These should not happen normally */
    PRESENT 'b'0100
        NEXT 'b'0000;
    PRESENT 'b'0101
        NEXT 'b'0000;
    PRESENT 'b'0110
        NEXT 'b'0000;
    PRESENT 'b'0111
        NEXT 'b'0000;
    PRESENT 'b'1000
        NEXT 'b'0000;
    PRESENT 'b'1001
        NEXT 'b'0000;
    PRESENT 'b'1010
        NEXT 'b'0000;
    PRESENT 'b'1011
        NEXT 'b'0000;
}

RD =  R_nW & SELECT & DS & Ca;
WR = !R_nW & SELECT & DS;

CS1 = SELECT & !A4;
CS3 = SELECT & A4;

USEL = SELECT & UDS;
LSEL = SELECT & LDS;

VPA = 'b'1;
VPA.oe = (FC2 & FC1 & FC0) & (!A3 & A2 & A1);